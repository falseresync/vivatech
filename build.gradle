import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'com.gradleup.shadow' version '9.0.0-beta10'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven { url = 'https://maven.shedaniel.me/' }
	maven { url = 'https://maven.ladysnake.org/releases' }
	maven { url = 'https://maven.terraformersmc.com/' }
	maven { url = 'https://maven.wispforest.io' }
	maven {
		url = "https://maven2.bai.lol"
		content {
			includeGroup "lol.bai"
			includeGroup "mcp.mobius.waila"
		}
	}
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://jitpack.io"
	}
}

fabricApi {
	configureDataGeneration()
}

loom {
	accessWidenerPath = file("src/main/resources/vivatech.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// ------------------------
	// --- MOD DEPENDENCIES ---
	// ------------------------
	modApi 'me.shedaniel.cloth:cloth-config-fabric:15.0.140'
	modApi "dev.emi:trinkets:3.10.0"

	// --------------------------
	// --- OTHER DEPENDENCIES ---
	// --------------------------
	implementation 'org.jgrapht:jgrapht-core:1.5.2'
	shadow 'org.jgrapht:jgrapht-core:1.5.2'

	// -----------------------------------
	// --- COMPATIBILITY & CONVENIENCE ---
	// -----------------------------------
	// Comment out / uncomment for testing and convenience
	modCompileOnly "mcp.mobius.waila:wthit-api:fabric-$wthit_version"
	modLocalRuntime "mcp.mobius.waila:wthit:fabric-$wthit_version"
	modLocalRuntime "lol.bai:badpackets:fabric-$badpackets_version"

	modCompileOnly "dev.emi:emi-fabric:$emi_version:api"
	modLocalRuntime "dev.emi:emi-fabric:$emi_version"

	modCompileOnly "com.terraformersmc:modmenu:$modmenu_version"
	modLocalRuntime "com.terraformersmc:modmenu:$modmenu_version"

	modLocalRuntime "maven.modrinth:betterf3:11.0.3"
	modLocalRuntime "maven.modrinth:component-viewer:1.2.0+1.21.1"
	modLocalRuntime "com.github.provismet:lilylib:1.3.1-mc1.21" // required by tooltip-scroll
	modLocalRuntime "maven.modrinth:tooltip-scroll:1.4.0+1.21"
	modLocalRuntime "maven.modrinth:dev-tools-unlocker:1.0.0"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}
tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(ShadowJar).configureEach {
	dependsOn('jar')

	configurations = [project.configurations.shadow]
	enableRelocation = true
	relocationPrefix = "falseresync.shadowed"
	minimize()
	exclude('.cache/**')

	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

tasks.withType(RemapJarTask).configureEach {
	dependsOn('shadowJar')
	mustRunAfter('shadowJar')
	inputFile = shadowJar.archiveFile.get()
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}
